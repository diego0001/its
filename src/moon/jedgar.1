TITLE WATCH

A=1
B=2
C=3
D=4
T=5
TT=6
P=7
U=10
N=11
CH=16
CH2=17

SYMS:	SQUOZE 0,USRHI
USRHI:	0
	SQUOZE 0,LUBLK
LUBLK:	0
	SQUOZE 0,FLSINS
FLSINS:	10,,0
	SQUOZE 0,UNAME
UNAME:	10,,0
	SQUOZE 0,EPDL
EPDL:	10,,0
	SQUOZE 0,SUPPRO
SUPPRO:	10,,0
	SQUOZE 0,TTYTBL
TTYTBL:	10,,0
	SQUOZE 0,UHANG
UHANG:	0
	SQUOZE 0,TOOP
TOOP:	11,,0
	SQUOZE 0,UMAPS
UMAPS:	10,,0
	SQUOZE 0,UPGMP
UPGMP:	10,,0
	SQUOZE 0,USTP
USTP:	10,,0
SYMP:	-30,,SYMS
BADGE:	0
PDL:	-40,,PDL
	BLOCK 40
JEDGAR:	MOVE P,PDL
	MOVEI T,-1
	TDNE T,USRHI
	JRST RESTRT
	MOVE T,SYMP
EVAL:	MOVE TT,(T)
	.EVAL TT,
	 JFCL
	HRRM TT,1(T)
	AOBJN T,.+1
	AOBJN T,EVAL

RESTRT:	.OPEN CH,[SIXBIT /  #TTY/]
	 .VALUE
	MOVEI T,CH
	.SUSET [116,,T]
	LDB N,[220600,,T]
	MOVE T,ASKPTR
	.IOT CH,T
	.CLOSE CH,
	MOVEI T,401001
ABSCOR:	MOVSI TT,1000
	IOR TT,T
	.CBLK TT,
	 JFCL
	ADDI T,1001
	TRNN T,400
	 JRST ABSCOR
	.OPEN CH,[SIXBIT /   TTY/]
	 .VALUE
	.IOT CH,T
	SETZM BADGE
	CAIE T,171
	 CAIN T,131
	  SETOM BADGE
	.CLOSE CH,
	.VALUE [ASCIZ /:PROCED
/]
	
LOOP:	SETZM U
NXTUSR:	CAMG U,@USRHI
	 JRST LOOKU
	MOVEI T,74
	.SLEEP T,
	JRST LOOP
LOOKU:	SKIPE @USTP
	 JRST NXUSR
	MOVEI TT,@UHANG
	HRLI TT,260640
	CAME TT,@FLSINS
	 JRST NXUSR
	HRRZ A,@EPDL
	MOVE B,A
	LSH B,-12
	ROT B,-1
	SKIPE @UMAPS
	 JRST NXUSR
	MOVEI TT,@UPGMP
	ADD TT,B
	SKIPGE B
	 SKIPA B,(TT)
	MOVS B,(TT)
	TRNN B,600000
	JRST NXUSR
	ANDI B,777
	LSH B,12
	ANDI A,1777
	IOR A,B
	CAIN A,@TOOP
	 JRST CAUGHT
NXUSR:	ADDI U,@LUBLK
	JRST NXTUSR
CLIOPN:	SIXBIT /  'CLI/
	SIXBIT /UNAME /
	SIXBIT /HACTRN/
CLIMES:	ASCII / You are the subject of an illegal wiretap by
convicted Watergate conspirator /
CLINAM:	0
	0
CLIPTR:	-22,,CLIMES
CLIMS2:	ASCII /
The miscreant has been executed. /
CLIPT2:	-7,,CLIMS2
ASKMES:	ASCII /Am I licensed to kill?   /
ASKPTR:	-5,,ASKMES

CAUGHT:	MOVE TT,@UNAME
	MOVE B,[440700,,CLINAM]
CT1:	SETZM T
	LSHC T,6
	ADDI T,40
	IDPB T,B
	JUMPN TT,CT1
	MOVEI T,42
CT2:	IDPB T,B
	MOVEI T,40
	CAME B,[010700,,CLINAM+1]
	 JRST CT2
	.SUSET [4,,CLIOPN+1]
	.OPEN CH,CLIOPN
	 .VALUE
	MOVE T,CLIPTR
	.IOT CH,T
	SKIPE BADGE
	 JRST JUSTIC
DEPUTY:	.CLOSE CH,
	.BREAK 16,60000
JUSTIC:	PUSHJ P,SPCTTY
WHO1:	MOVE B,@SUPPRO
	CAME B,[-1]
	 JRST [ HRR U,B
		JRST WHO1 ]
	HRRZ D,U
	IDIVI D,@LUBLK
	JRST PATCH
	JFCL
PRETT:	MOVEI T,132
	.SLEEP T,
	MOVEI C,(SIXBIT /T00/)
	JUMPL A,JUST00
	LSHC A,-3
	LSH A,3
	LSHC A,3
	IOR C,A
	HRLI C,C
	.OPEN CH2,C
	 JRST JUST00
	MOVE T,MRLPTR
	.IOT CH2,T
	.CLOSE CH2,
JUST00:	MOVE T,CLIPT2
	.IOT CH,T
	.CLOSE CH,
	JRST LOOP
MRLMES:	ASCII /Crime does not pay
/
MRLPTR:	-4,,MRLMES

SPCTTY:	MOVE A,@TTYTBL
	JUMPGE A,SPCGOT
SPCUP:	MOVE B,@SUPPRO
	CAME B,[-1]
	 JRST [ HRR U,B
		JRST SPCUP ]
SPCDWN:	MOVE A,@TTYTBL
	JUMPGE A,SPCGOT
	HRR U,A
	TLNN A,200000
	JRST SPCDWN
	MOVSI A,-1
SPCGOT:	POPJ P,

PATCH":	MOVE T,@UNAME
	.WSNAM T,
	JFCL
	JFCL
	JRST [ IORI D,400000
		.CALL [ SETZ
			'DETACH
			SETZ D ]
		JFCL
		JRST PRETT ]
	.CLOSE
	JRST DEPUTY

UDIRF:	SIXBIT /   DSK/
	SIXBIT /.FILE./
	SIXBIT /(DIR)/

END JEDGAR

